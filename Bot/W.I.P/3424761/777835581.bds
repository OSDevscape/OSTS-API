$try
$nomention
$if[$checkUserPerms[$authorID;moderatemembers]==false]
$ephemeral
You require the **moderate members** permission to use this command
$elseif[$checkUserPerms[$botID;moderatemembers]==false]
$ephemeral
I require the **moderate members** permission to ban users
$elseif[$guildID==]
$ephemeral
This command cannot be used in DMâ€™s
$elseif[$message[user]==$authorID]
$ephemeral
You cannot timeout yourself
$elseif[$message[user]==$botID]
$ephemeral
You cannot timeout me
$elseif[$and[$rolePosition[$highestRole[$message[user]]]<$rolePosition[$highestRole[$authorID]];$serverOwner!=$authorID]==true]
$ephemeral
$username[$message[user]] is higher in the role hierarchy than you, therefore you cannot timeout them
$elseif[$and[$rolePosition[$highestRole[$message[user]]]==$rolePosition[$highestRole[$authorID]];$serverOwner!=$authorID]==true]
$ephemeral
You are not higher in the role hierarchy than $username[$message[user]], therefore you cannot timeout them
$elseif[$rolePosition[$highestRole[$message[user]]]<$rolePosition[$highestRole[$botID]]]
$ephemeral
$username[$message[user]] is higher in the role hierarchy than me, therefore I cannot timeout them
$elseif[$rolePosition[$highestRole[$message[user]]]==$rolePosition[$highestRole[$authorID]]]
$ephemeral
I am not higher in the role hierarchy than $username[$message[user]], therefore I cannot timeout them
$elseif[$isBot[$message[user]]==true]
$ephemeral
You cannot timeout bots
$elseif[$isNumber[$replaceText[$replaceText[$replaceText[$replaceText[$replaceText[$message[duration];s;];m;];h;];d;];w;]]==false]
$ephemeral
Please enter a valid duration for the timeout
$else
$try
$timeout[$message[duration];$message[user]]
$ephemeral
Successfully timed out **$username[$message[user]]**
$catch
$ephemeral
The timeout duration cannot be longer than 28 days
$endtry
$endif
$catch
$ephemeral
An error occurred:
$error[message]
$endtry